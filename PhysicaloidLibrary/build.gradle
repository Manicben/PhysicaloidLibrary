plugins {
    id 'com.android.library'
    id 'maven-publish'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group = 'com.manicben'
version = '1.0'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation('com.google.guava:guava:30.1.1-android')
}

// Build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile

    exclude "**/R**"
    exclude "**/BuildConfig*"
}

// Build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.manicben'
                artifactId = 'PhysicaloidLibrary'
                version = '1.0'
            }
        }
    }
}
